#compdef feed
#
# For my CLI or EDITOR file to REPL replayer/interactor util. Will likey
# need something like the following zstyle lines set in .zshrc to
# complete only specific REPL:
#
#   zstyle ':completion:*:*:feed:*:commands' fake-always clisp expect gdb perl sbcl tclsh tinyrepl wish zsh
#   zstyle ':completion:*:*:feed:*:commands' ignored-patterns '*'

local curcontext="$curcontext" state ret=1

_arguments \
  '1:file:->filesorstdin' \
  '2:command: _command_names -e' \
  '*::args:->command' && ret=0

case "$state" in
  filesorstdin)
    # if appear to be being piped to, complete stdin (-) as filename
    if [[ $BUFFER =~ "\|[[:blank:]]*$_comp_command" ]]; then
      _values 'stdin' - && ret=0
    else
      _files && ret=0
    fi
  ;;
  command)
    shift 1 words
    (( CURRENT-- ))
    _normal && ret=0
  ;;
esac

return ret
