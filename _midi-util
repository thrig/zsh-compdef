#compdef midi-util
#
# ZSH completion for midi-util. Install to a $fpath directory.

_midi-util() {
  _arguments \
    '*::midi-util command:_midiutil_mode'
}

_midiutil_mode() {
  _midiutil_modes=(
    'decompose:textify one way' \
    'dump:textify another way' \
    'nixcc:omit control changes'
  )

  if (( CURRENT == 1 )); then
    _describe -t commands 'midiutil command' _midiutil_modes
  else
    local curcontext="$curcontext"

    cmd="${_midiutil_modes[(r)$words[1]:*]%%:*}"
    if (( $#cmd )); then
      curcontext="${curcontext%:*:*}:midiutil-${cmd}:"
      _call_function ret _midiutil_$cmd || _message 'no more arguments'
    else
      _message "unknown midiutil command: $words[1]"
    fi
    return ret
  fi
}

_midiutil_decompose() {
  _arguments -s '1:file:_files'
}
_midiutil_dump() {
  _arguments -s '1:file:_files'
}
_midiutil_nixcc() {
  _arguments -s '1:file:_files' '2:file:_files'
}
