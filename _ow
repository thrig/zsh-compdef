#compdef ow wv

local curcontext="$curcontext" state ret=1

# --directory is not offered for completion on assumption 'wv' name or
# an alias will be used to obtain that mode
_arguments \
  '(- *)'{-h,--help}'[display help and exit]' \
  {-l,--list}'[lists the generated URL on stdout]' \
  {-o,--open=}'[open with browser]:method:->browser' \
  '*:something:->something' && ret=0

case "$state" in
  browser)
    _values "browser" ${(u)${(R)${(f)"$(<~/.ow/browsers)"}:#\#*}%%[[:blank:]]*}
    break
  ;;
  something)
    if [[ $name == ow ]]; then
      _alternative \
        'shortcut:shortcut:_values shortcut ${(u)${(R)${(f)"$(<~/.ow/shortcuts)"}:#\#*}%%[@[:blank:]]*}' \
        'files:filename:_files -g "*.html"'
    elif [[ $name == wv ]]; then
      _files
    fi
    break
  ;;
esac

return ret
